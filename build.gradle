/*
 * Copyright 2015-2016 Red Hat, Inc. and/or its affiliates
 * and other contributors as indicated by the @author tags.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.3.1"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.0"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "com.android.application"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 0
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 16
        targetSdkVersion 22

        resConfigs "en", "de"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def serverProperties = readProperties("server.properties")

        buildConfigField "String", "UPS_URL", formatStringField(readProperty(serverProperties, "ups.url"))
        buildConfigField "String", "UPS_VARIANT", formatStringField(readProperty(serverProperties, "ups.variant"))
        buildConfigField "String", "UPS_SECRET", formatStringField(readProperty(serverProperties, "ups.secret"))
        buildConfigField "String", "GCM_SENDER", formatStringField(readProperty(serverProperties, "gcm.sender"))

        signingConfigs {
            release {
                if (file("signing.properties").exists()) {
                    def signingProperties = readProperties("signing.properties")

                    storeFile file(readProperty(signingProperties, "keystore.file"))
                    storePassword readProperty(signingProperties, "keystore.password")
                    keyAlias readProperty(signingProperties, "key.alias")
                    keyPassword readProperty(signingProperties, "key.password")
                }
            }
        }

        buildTypes {
            debug {
                minifyEnabled true

                proguardFiles readProguardFiles()
            }

            release {
                signingConfig signingConfigs.release

                minifyEnabled true
                shrinkResources true

                proguardFiles readProguardFiles()
            }
        }
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

def readProperties(propertiesFileName) {
    def properties = new Properties()
    def propertiesFile = file(propertiesFileName)

    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newInputStream())
    }

    return properties
}

def readProperty(properties, propertyName) {
    def property = properties[propertyName]

    if (property) {
        return property
    } else {
        return ""
    }
}

def formatStringField(field) {
    return "\"${field}\""
}

def readProguardFiles() {
    def proguardFiles = []

    proguardFiles << android.getDefaultProguardFile("proguard-android.txt")

    file("proguard").eachFile { proguardFile ->
        proguardFiles << proguardFile
    }

    return proguardFiles
}

dependencies {
    repositories {
        mavenCentral()

        maven {
            url "https://clojars.org/repo/"
        }
    }

    compile "com.android.support:appcompat-v7:22.2.1"
    compile "com.android.support:design:22.2.1"
    compile "com.android.support:support-annotations:22.2.1"
    compile "com.android.support:support-v4:22.2.1"
    compile "com.github.lecho:hellocharts-library:1.5.6@aar"
    compile "com.google.android.gms:play-services:6.1.11"
    compile "com.google.zxing:android-integration:3.2.1"
    compile "com.jakewharton:butterknife:7.0.1"
    compile "com.jakewharton.timber:timber:3.1.0"
    compile "info.metadude.android:typed-preferences:2.0.0"
    compile "org.jboss.aerogear:aerogear-android-authz:2.1.0"
    compile "org.jboss.aerogear:aerogear-android-pipe:2.1.1"
    compile "org.jboss.aerogear:aerogear-android-push:2.2.1"
    compile "com.squareup:otto:1.3.8"
    compile "frankiesardo:icepick:3.1.0"

    provided "frankiesardo:icepick-processor:3.1.0"

    androidTestCompile "com.android.support:support-annotations:22.2.1"
    androidTestCompile "com.android.support.test:rules:0.3"
    androidTestCompile "com.android.support.test:runner:0.3"
    androidTestCompile "com.squareup.assertj:assertj-android:1.1.0"
}
